const int SPR_AMBUSH_SPAWN = 22;//Sprite to display, when enemy spawn from his hiding spot on ambush.

//Ambush. Spawns an enemy, when Link is near.
//Place FFC at enemy hiding spot.
//D0 - ID of enemy
//D1 - detection radius.
//D2 & D3 - size offset for larger enemies.
//D4 - Set to -1 - replace combo underneath FFC with screen`s undercombo, or
//     positive number to replace destroyed combo with ID, using FFC`s CSet.
//D5 - Sound to play on ambush.

ffc script Ambush{
	void run (int enemyID, int radius, int sizex, int sizey, int undercombo, int sound){
		int cmb = ComboAt(CenterX(this), CenterY(this));
		if (sizex==0) sizex=1;
		if (sizey==0) sizey=1;
		while(Distance(Link->X, Link->Y, this->X, this->Y)>radius)Waitframe();
		Game->PlaySound(sound);
		npc en = CreateNPCAt(enemyID, this->X-8*(sizex-1), this->Y-8*(sizey-1));
		lweapon l = CreateLWeaponAt(LW_SPARKLE, this->X, this->Y);
		l->UseSprite(SPR_AMBUSH_SPAWN);
		l->NumFrames=3;
		l->ASpeed=30;
		l->CollDetection=false;
		if (undercombo>0){
			Screen->ComboD[cmb]= undercombo;
			Screen->ComboC[cmb] = this->CSet;
		}
		else if (undercombo==-1){
			Screen->ComboD[cmb]= Screen->UnderCombo;
			Screen->ComboC[cmb] = Screen->UnderCSet;
		}
	}
}