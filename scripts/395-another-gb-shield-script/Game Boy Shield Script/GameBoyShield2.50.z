//Script originally by Elmensajero. 

import "include/std.zh"

//Change these values to match your item setup
const int GBS_SMALL=93;
const int GBS_MAGIC=8;
const int GBS_MIRROR=37;
const int GBS_FAKESMALL=123;
const int GBS_FAKEMAGIC=124;
const int GBS_FAKEMIRROR=125;
const int SFX_GBSHIELD=65;

int ButtonPressed;
bool shieldon;
int lastdir;
int gbsound;

global script Slot_2{
    void run(){
       while(true){
			GBShield();
            Waitdraw();
			GBShieldDraw();
            Waitframe();
        }
    }
}

void GBShield(){
	lastdir=Link->Dir; //Direction to face when strafing
	if((ButtonPressed==1 && Link->InputA && !shieldon)
	|| (ButtonPressed==2 && Link->InputB && !shieldon)){ //Enable shield when button is pressed
		shieldon=true;
		gbsound=SFX_GBSHIELD;
		
		//If the Link has the fake shield item, give him the real shield...
		if(Link->Item[GBS_FAKEMIRROR]) Link->Item[GBS_MIRROR]=true;
		else if(Link->Item[GBS_FAKEMAGIC]) Link->Item[GBS_MAGIC]=true;
		else if(Link->Item[GBS_FAKESMALL]) Link->Item[GBS_SMALL]=true;
		else{
			gbsound=0;
			shieldon=false;
		}
		if(gbsound>0) Game->PlaySound(SFX_GBSHIELD);
	}
	else if((ButtonPressed==1 && !Link->InputA && shieldon)
	|| (ButtonPressed==2 && !Link->InputB && shieldon)){ //Remove shield when button is released
		Link->Item[GBS_MIRROR]=false;
		Link->Item[GBS_MAGIC]=false;
		Link->Item[GBS_SMALL]=false;
		shieldon=false;
		ButtonPressed = 0;
	}
}

void GBShieldDraw(){
     if(ButtonPressed>0 && shieldon 
	 && Link->Action!=LA_SWIMMING 
	 && Link->Action!=LA_DROWNING 
	 && Link->Action!=LA_ATTACKING 
	 && Link->Action!=LA_CHARGING 
	 && Link->Action!=LA_SPINNING){ //Strafe while the button is held down              
          Link->Dir=lastdir;
     }
}

item script GB_Shield{
	void run(){
		if(Link->InputA) ButtonPressed = 1;
		else if(Link->InputB) ButtonPressed = 2;
	}
}